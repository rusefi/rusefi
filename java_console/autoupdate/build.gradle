
apply from: '../../java_tools/dependencies.gradle'

configurations {
    all*.exclude group: 'org.hamcrest', module: 'hamcrest-core'
}

dependencies {
    api global_libs.javaxJson
    api project(':core_ui')

    api global_libs.jcip
    api global_libs.json

    // xml binding drama #7430 we do not understand this hack but it seems to help :(
    api global_libs.jaxb

    // not 'api' since Android would use own implementation
    implementation ts_plugin_libs.httpclient

    // junit 4.13 does not mix well with httpclient :(
    testImplementation group: 'junit', name: 'junit', version: '4.8.2'
    testImplementation global_libs.mockito
}

// yes nasty sorry
evaluationDependsOn(':core_ui')
evaluationDependsOn(':core_io')

jar {
    base {
        archivesName = 'rusefi_autoupdate'
    }
    destinationDirectory = file("$rootDir/../console")

    // lame 'shadowJar' manual implementation causes logo.png to be include both as source ans outputs? weird
    // todo: why do we not use proper shadowJar here?
    duplicatesStrategy = DuplicatesStrategy.WARN
    from project.sourceSets.main.allSource
    from project(':core_ui').sourceSets.main.output
    from project(":core_io").sourceSets.main.output
    from project(':logging-api').sourceSets.main.output

    manifest {
        attributes(
                'Main-Class': 'com.rusefi.autoupdate.Autoupdate',
                'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Built-Date': new Date().toString()
        )
    }
}
