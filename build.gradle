plugins {
  id 'com.gradleup.shadow' version "${shadowVersion}"  apply false
}

apply from: 'dependencies.gradle'

allprojects {
    repositories {
        mavenCentral()
    }

    plugins.withType(JavaPlugin) {
        java {
          // TS still comes with 1.8 and we have a plugin, lets build compatible
          sourceCompatibility = '8'
          targetCompatibility = '8'
          // [tag:java8]
          toolchain {
            languageVersion.set(JavaLanguageVersion.of(11))
          }
        }

        dependencies {
            implementation global_libs.annotations
            testImplementation global_libs.junit5
            testImplementation global_libs.junit5engine
        }

      test {
        // Enable JUnit 5
        useJUnitPlatform()
      }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    tasks.withType(JavaExec).configureEach {
        if (name.endsWith("main()")) {
            // https://github.com/gradle/gradle/issues/21364
            notCompatibleWithConfigurationCache("JavaExec created by IntelliJ")
        }
    }
}

tasks.named('wrapper') {
    distributionType = Wrapper.DistributionType.ALL
}

// gradlew allDeps
subprojects {
    task allDeps(type: DependencyReportTask) {}

  apply plugin: 'java-library'
  apply plugin: 'java-test-fixtures'
  apply plugin: 'com.gradleup.shadow'
}
